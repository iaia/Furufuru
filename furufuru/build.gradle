plugins {
    id("com.android.library")
    id("kotlin-android")
    id("kotlin-kapt")
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.10'
    id("de.mannodermaus.android-junit5")
    id("maven-publish")
    id("signing")
    id("org.jetbrains.kotlin.plugin.compose") version "2.0.0"
}

def FURUFURU_VERSION_NAME = '0.11.0'
def FURUFURU_VERSION_CODE = 110

group = "dev.iaiabot.furufuru"
archivesBaseName = "furufuru"
version = FURUFURU_VERSION_NAME

android {
    compileSdk 34
    namespace 'dev.iaiabot.furufuru'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField("long", "VERSION_CODE", "${FURUFURU_VERSION_CODE}")
        buildConfigField("String", "VERSION_NAME", "\"${FURUFURU_VERSION_NAME}\"")
    }

    buildTypes {
        debug {
            buildConfigField("String", "GITHUB_API_URL", "\"https://api.github.com\"")
        }

        release {
            buildConfigField("String", "GITHUB_API_URL", "\"https://api.github.com\"")
        }
    }

    buildFeatures {
        buildConfig true
        compose true
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // Add Kotlin source directory to all source sets
    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.core:core-ktx:1.13.1"

    // ViewModel
    ext.lifecycle_version = "2.8.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // Lifecycles only (without ViewModel or LiveData)
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.22"

    implementation 'com.google.android.material:material:1.12.0'

    // koin
    ext.koin_version = '3.5.0'
    // Koin main features for Android (Scope,ViewModel ...)
    implementation "io.insert-koin:koin-android:$koin_version"
    // Koin Java Compatibility
    implementation "io.insert-koin:koin-android-compat:$koin_version"
    // Koin for Jetpack WorkManager
    implementation "io.insert-koin:koin-androidx-workmanager:$koin_version"
    // Koin for Jetpack Compose
    implementation "io.insert-koin:koin-androidx-compose:$koin_version"

    // permission dispatcher
    ext.permission_dispatcher_version = '4.9.2'
    implementation "com.github.permissions-dispatcher:permissionsdispatcher:${permission_dispatcher_version}"
    kapt "com.github.permissions-dispatcher:permissionsdispatcher-processor:${permission_dispatcher_version}"

    // kotlin-serialization
    implementation("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-properties:1.6.0'
    implementation('com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:1.0.0')

    // retrofit
    ext.retrofit_version = '2.9.0'
    implementation("com.squareup.retrofit2:retrofit:$retrofit_version")
    // implementation("com.squareup.retrofit2:retrofit-mock:$retrofit_version")

    // retrofitが以下の問題のあるlibに依存しているため
    // om.squareup.okhttp3/okhttp@3.14.9
    // com.squareup.okio/okio@3.2.0
    implementation("com.squareup.okio:okio:3.6.0")
    implementation("com.squareup.okhttp3:okhttp:4.11.0")

    implementation platform('androidx.compose:compose-bom:2024.05.00')
    implementation "androidx.compose.ui:ui"
    // Tooling support (Previews, etc.)
    implementation "androidx.activity:activity-compose"
    implementation "androidx.compose.ui:ui-tooling"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation"
    // Material Design
    implementation "androidx.compose.material:material"
    // Material design icons
    implementation "androidx.compose.material:material-icons-core"
    implementation "androidx.compose.material:material-icons-extended"
    // Integration with observables
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.runtime:runtime-rxjava2"

    testImplementation 'junit:junit:4.13.2'
    testImplementation(platform('org.junit:junit-bom:5.10.0'))
    testImplementation('org.junit.jupiter:junit-jupiter:5.10.0')
    testRuntimeOnly("org.junit.platform:junit-platform-launcher") {
        because("Only needed to run tests in a version of IntelliJ IDEA that bundles older versions")
    }
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine")
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'

    testImplementation "android.arch.core:core-testing:1.1.1"

    // mockk
    ext.mockk = "1.12.3"
    testImplementation "io.mockk:mockk:${mockk}"
    testImplementation "io.mockk:mockk-agent-jvm:${mockk}"

    // spek
    ext.spek = "2.0.17"
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek"

    // spek requires kotlin-reflect, omit when already in classpath
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // kotest
    ext.kotest_version = "5.4.2"
    testImplementation "io.kotest:kotest-runner-junit5:$kotest_version"

    // truth
    testImplementation 'com.google.truth:truth:1.1.5'

    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("source")
}
tasks.register('javadoc', Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}
tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from tasks.javadoc.destinationDir
}
tasks.register('androidSourcesJar', Jar) {
    dependsOn generateMetadataFileForReleasePublication
    dependsOn javadocJar
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }

        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId 'dev.iaiabot.furufuru'
                artifactId 'furufuru'
                version FURUFURU_VERSION_NAME

                from components.release

                // artifact androidSourcesJar
                // artifact javadocJar

                // Mostly self-explanatory metadata
                pom {
                    name = 'furufuru'
                    description = 'Make an issue on github easily just by shaking.'
                    url = 'https://github.com/iaia/furufuru'

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'iaia'
                            name = 'iaia'
                            email = 'iaia72160@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/iaia/Furufuru.git'
                        developerConnection = 'scm:git:ssh://github.com/iaia/Furufuru.git'
                        url = 'https://github.com/iaia/Furufuru'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}
